buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath "gradle.plugin.se.bjurr.gitchangelog:git-changelog-gradle-plugin:1.65"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'se.bjurr.gitchangelog.git-changelog-gradle-plugin'

version = project.output_version
group = 'com.github.percivalalb' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'DoggyTalents-1.16.5'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
        content {
            includeGroup "mezz.jei"
        }
    }
    maven {
        url "https://modmaven.k-4u.nl"
        content {
            includeGroup "mezz.jei"
        }
    }
}

sourceSets {
    api
    main {
        resources.srcDirs += 'src/generated/resources'
    }
    test
}

configurations {
    apiCompile.extendsFrom(compile)
}

minecraft {
    mappings channel: 'snapshot', version: project.mcp_mappings

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            taskName 'doggytalents_client'
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods {
                doggytalents {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }

        server {
            taskName 'doggytalents_server'
            workingDirectory project.file('run-server')
            property 'forge.logging.console.level', 'debug'
            mods {
                doggytalents {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }

        data {
            taskName 'doggytalents_data'
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'doggytalents', '--all', '--output', '"' + file('src/generated/resources/') + '"', '--existing', '"' + sourceSets.main.resources.srcDirs[0] + '"'
            mods {
                doggytalents {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.forge_version}"

    implementation sourceSets.api.output

    compileOnly fg.deobf("mezz.jei:jei-${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_version}")

    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.1")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.1")
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allJava
    from sourceSets.api.allJava
}

task apiJar(type: Jar) {
    classifier = "api"
    from sourceSets.api.output
    from sourceSets.api.allJava
}

artifacts {
    archives sourcesJar
    archives apiJar
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    from sourceSets.main.output.classesDirs
    from sourceSets.api.output.classesDirs
    from sourceSets.main.output.resourcesDir
    from sourceSets.api.output.resourcesDir

    manifest {
        attributes([
            "Specification-Title": "Doggy Talents",
            "Specification-Vendor": "percivalalb",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"percivalalb",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

test {
    useJUnitPlatform()
}

import se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask
task makeChangelog(type: GitChangelogTask) {
    file = new File("build/changelog.html")
    untaggedName = "Current release ${project.version}"
    fromCommit = 'f594ba81ba02d04a2189b6cf15216d2afb6943ba'
    toRef = 'HEAD'

    templateContent = file('changelog.mustache').getText('UTF-8')
}

curseforge {
    apiKey = findProperty('curseforge_api_key') ?: 0
    project {
        id = project.curseforge_project_id
        changelog = file("build/changelog.html")
        changelogType = 'html'
        releaseType = findProperty("curseforge_release_type") ?: 'alpha'
        addArtifact apiJar
    }
}

// Must be done in afterEvaluate as the specific curseforge$id
// task is generated lazily
afterEvaluate {
    tasks.named("curseforge${project.curseforge_project_id}").get().dependsOn.add(makeChangelog)
}

if (project.hasProperty('UPDATE_MAPPINGS')) {
    extractRangeMap {
        sources sourceSets.api.java.srcDirs
    }
    applyRangeMap {
        sources sourceSets.api.java.srcDirs
    }
    sourceSets.api.java.srcDirs.each { extractMappedNew.addTarget it }
}
